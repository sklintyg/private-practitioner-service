plugins {
    id "io.spring.dependency-management" version "1.1.6" apply false
    id 'test-report-aggregation'
    id 'org.sonarqube' version '6.3.1.5724'
    id "org.owasp.dependencycheck" version "12.1.6"
    id 'jacoco'
}

ext {
    intygBomVersion = System.properties['intygBomVersion'] ?: '0.1.0.3'
    infraVersion = System.properties['infraVersion'] ?: '4.0.0-SNAPSHOT'

    ppSchemasVersion = "1.0.5.2"
    schemasContractVersion = "2.1.8.2"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    java {
        sourceCompatibility = 21
        targetCompatibility = 21
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**",
                ])
            }))
        }
    }

    tasks.withType(Test).configureEach {
        jvmArgs '-Xshare:off'
    }
}

group = 'se.inera.private-practitioner-service'
version = System.properties['buildVersion'] ?: '0-SNAPSHOT'


reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-private-practitioner-service"
        property "sonar.projectName", "intyg-private-practitioner-service"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}