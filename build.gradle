plugins {
    alias(libs.plugins.org.springframework.boot)
    alias(libs.plugins.org.sonarqube)
    alias(libs.plugins.org.owasp.dependencycheck)
    alias(libs.plugins.com.github.ben.manes.versions)
    id 'test-report-aggregation'
    id 'application'
}

ext {
    intygBomVersion = System.properties['intygBomVersion'] ?: '0.0.1-SNAPSHOT'

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

application {
    mainClassName = 'se.inera.intyg.privatepractitionerservice.PrivatePractitionerServiceApplication'
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'java-test-fixtures'

    java {
        sourceCompatibility = 21
        targetCompatibility = 21
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testFixturesImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**",
                ])
            }))
        }
    }

    tasks.withType(Test).configureEach {
        jvmArgs '-Xshare:off'
    }
}

group = 'se.inera.private-practitioner-service'
version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

reporting {
    reports {
        integrationTestAggregateTestReport(AggregateTestReport) {
            testSuiteName = 'integrationTest'
        }
    }

    dependencies {
        testReportAggregation project(':app')
        testReportAggregation project(':integration-api')
        testReportAggregation project(':integration-intyg-proxy-service')
        testReportAggregation project(':integration-test')
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-private-practitioner-service"
        property "sonar.projectName", "intyg-private-practitioner-service"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = ['HTML', 'JSON']
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
    analyzers.setNodeEnabled(false)
    analyzers.setCentralEnabled(false)
    analyzers.ossIndex.setUsername(project.properties.get('ossIndexUsername'))
    analyzers.ossIndex.setPassword(project.properties.get('ossIndexPassword'))
}